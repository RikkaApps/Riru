apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "rikka.riru"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName "${riruApiVersion}.${versionNameMinor}.${versionNamePatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude '/META-INF/*.version'
        exclude '/META-INF/*.kotlin_module'
        exclude '/kotlin/**'
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo.includeInApk false
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "riru.apk"
        if (variant.getBuildType().isMinifyEnabled()) {
            def outDir = new File(rootDir, "out")
            variant.assembleProvider.get().doLast {
                copy {
                    from outputFile
                    into outDir
                }
            }
        }
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.annotation:annotation:1.2.0"

    compileOnly project(':stub')

    def libsuVersion = '3.1.1'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:io:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
}

apply from: project.file('signing.gradle')
